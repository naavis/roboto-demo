INCLUDE "Hardware.inc"
INCLUDE "rgbgrafx/rgbgrafx.inc"
INCLUDE "gbt_player/gbt_player.inc"
INCLUDE "imagedata/skrolli_logo.inc"

Section "V-Blank ISR",HOME[$40]
  jp VBlank

Section "Initializer",HOME[$100]
  nop
  jp main

Section "MainCode",HOME[$150]
main:
  ; Set all palette colors to black for the duration of loading image data
  RGBG_WaitForVRAM
  ld A, $FF
  ld [rBGP], A

  ; Initialize high byte of the tileset address
  ld A, $80
  ld [RGBG_tileset], A
  ; Load tile patterns to VRAM
  ld BC, skrolli_tile_data
  ld D, $FF
  ld E, 0
  RGBG_WaitForVRAM
  call RGBG_LoadTiles

  ; Initialize high byte of the tilemap address
  ld A, $98
  ld [RGBG_tilemap], A

  ; Set tilemap offset to zero
  ld A, $00
  ld [RGBG_map_offset], A

  ; Load tile map
  RGBG_WaitForVRAM
  ld BC, skrolli_map_data
  ld H, $0
  ld L, $0
  ld E, skrolli_tile_map_width
  ld D, skrolli_tile_map_height
  call RGBG_SetTileMap

  ; Reset color palette to default
  RGBG_WaitForVRAM
  call RGBG_SetDefPals

  di ; Disable interrupts while setting them up
  ld SP, $FFFE ; Initialize stack pointer
  ld A, IEF_VBLANK
  ld [rIE], A

  ; Initialize music
  ld DE, skrolli_anthem_data
  ld BC, BANK(skrolli_anthem_data)
  ld A, $05
  call gbt_play ; Play song
  ei; Enable interrupts again

.loop:
  halt
  jr .loop


VBlank:
  call gbt_update ; Update music player
  reti
