INCLUDE "Hardware.inc"
INCLUDE "rgbgrafx/rgbgrafx.inc"

Section "Lissajous curve scene",CODE
scene_lissajous::
  di ; Disable interrupts during setup
  xor A
  ld [frame_number], A
  ld [rotation], A

  ; Set all palette colors to black for the duration of loading image data
  RGBG_WaitForVRAM
  ld A, $FF
  ld [rBGP], A
  ld [rOBP0], A

  ; Reset interrupt handlers
  call initialize_interrupts

  ; Enable V-blank interrupts
  ld A, IEF_VBLANK
  ld [rIE], A

  ; Configure LCD controller
  ld A, (LCDCF_ON | LCDCF_WINOFF | LCDCF_BG8000 | LCDCF_BGOFF | LCDCF_OBJ8 | LCDCF_OBJON)
  ld [rLCDC], A

  ; Initialize high byte of the tileset starting address
  ld A, $80
  ld [RGBG_tileset], A

  ; Load point sprite tile into VRAM
  ld BC, point_sprite
  ld D, 1
  ld E, 0
  call RGBG_LoadTiles
  call RGBG_ZeroOAM
  call RGBG_CopyDMARoutine

; Set all 40 sprites to show same pattern
  xor A
  ld B, A
.sprite_creation_loop:
  push AF
  call RGBG_SetSpriteTile
  pop AF
  inc A
  cp 40
  jr c, .sprite_creation_loop

; Set all sprites to 0,0
  xor A
  ld B, A
  ld C, A
.sprite_initial_position_loop:
  push AF
  call RGBG_SetSpritePos
  pop AF
  inc A
  cp 40
  jr c, .sprite_initial_position_loop

  RGBG_WaitVBL
  call RGBG_DoDMA ; Send sprite data to OAM

  ; Set interrupt handler
  ld BC, lissajous_vblank
  call set_vblank_interrupt

  ; Reset color palette to default
  RGBG_WaitForVRAM
  call RGBG_SetDefPals

  ei ; Enable interrupts again

.loop:
  halt
  jr .loop

lissajous_vblank:
  ld A, [rotation]
  ld H, A
  ld A, [frame_number] ; Get frame number to index LUTs
  ld D, A ; Save frame number to D
  ld E, 40 ; Use E as sprite counter

.loop_start:
  swap E
  ld A, D ; Load frame number to A
  add E
  add A
  add H
  call get_sine
  add 20
  ld B, A ; Set first sine value to X

  ld A, D ; Load frame number to A
  add E
  call get_sine ; Get sine value and save it to A
  add 20
  ld C, A ; Set second sine value to Y coordinate

  swap E
  ld A, E
  push HL
  call RGBG_SetSpritePos ; Save sprite position
  pop HL
  dec E
  jr nz, .loop_start
  push HL
  call RGBG_DoDMA ; Execute DMA
  pop HL

  ld A, D ; Return frame number to A
  inc A ; Increment frame number
  ld [frame_number], A ; Save frame number
  ld A, H
  inc A
  ld [rotation], A
  ret

; A = index from $00 to $FF
get_sine:
  push HL
  ld H, $2F
  ld L, A
  ld A, [HL]
  pop HL
  ret

point_sprite:
DW `00000000
DW `00000000
DW `00000000
DW `00033000
DW `00033000
DW `00000000
DW `00000000
DW `00000000

Section "Sine lookup-table",HOME[$2F00] ; Alignment to 00 important
sine_lut:
ANGLE   SET     0.0
        REPT    256
        DB      (MUL(64.0,SIN(ANGLE))+64.0)>>16
ANGLE   SET     ANGLE+256.0
        ENDR

Section "Lissajous variables",BSS
frame_number: DS 1
rotation: DS 1
